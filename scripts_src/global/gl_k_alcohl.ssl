#define SCRIPT_REALNAME "gl_k_alcohl"
#include "../headers/define.h"
#include "../headers/scripts.h"
#include "../headers/command.h"

procedure start;

variable count;
variable timer_shakes;
variable timer_relief;
variable msg;

#define SHAKES_TIME (2*ONE_GAME_DAY)
#define MEDIUM_ALCOHOL_USAGE_TIME (3*ONE_GAME_DAY)
#define SPORADIC_ALCOHOL_USAGE_TIME ONE_GAME_WEEK
#define DURATION_OF_ADDICTION ONE_GAME_WEEK
#define INTERFACE_TAG_ADDICT 4

#define MSG_FILE "k_alcohl.msg"
#define MSG_became_addicted 1
#define MSG_cravings 2
#define MSG_shakes 3
#define MSG_relieved 4

#define SGVAR_last_drink_date "k_alco_d"

/**
 * True is `x` is an alcohol bottle, False otherwise.
 * @arg {ObjPtr} x
 */
#define is_alcohol(x) \
    (  (obj_pid(x) == PID_BEER) \
    or (obj_pid(x) == PID_BOOZE) \
    or (obj_pid(x) == PID_GAMMA_GULP_BEER) \
    or (obj_pid(x) == PID_ABBEY_BOOZE) \
    or (obj_pid(x) == PID_ROENTGEN_RUM) \
    or (obj_pid(x) == PID_ROT_GUT) )

#define is_booze(x) \
    (  (obj_pid(x) == PID_BOOZE) \
    or (obj_pid(x) == PID_ABBEY_BOOZE) \
    or (obj_pid(x) == PID_ROENTGEN_RUM) \
    or (obj_pid(x) == PID_ROT_GUT) )

/**
 * Displays message `x` from **game** message file `msg`
 * @arg {int} x - message number
 */
#define display_mymsg(x) display_msg(message_str_game(msg, x))

/**
 * Makes target an addict if all checks pass.
 * @arg {ObjPtr} who - target critter
 * @arg {ObjPtr} item - item being used
 */
procedure drink(variable who, variable item) begin
    variable roll, chance;
    variable last_drink_date = get_sfall_global_int(SGVAR_last_drink_date);
    set_sfall_global(SGVAR_last_drink_date, game_time);

    // target is not dude
    if (who != dude_obj) then return;
    // not alcohol
    if not is_alcohol(item) then return;
    // already addicted
    if (global_var(GVAR_ALCOHOL_ADDICT) == 1) then return;

    // regular drinking increases chances
    if ((game_time - last_drink_date) >= SPORADIC_ALCOHOL_USAGE_TIME) then begin
        chance = 1;
        display_msg("<test> last drink longer than a week ago. Your base chance of addiction is :"+chance);
    end
    else if ((game_time - last_drink_date) >= MEDIUM_ALCOHOL_USAGE_TIME) then begin
        chance = 5;
        display_msg("<test> last drink longer than 3 days ago. Your base chance of addiction is :"+chance);
    end
    else begin
        chance = 10;
        display_msg("test last drink less than 3 days ago. Your base chance of addiction is :"+chance);
    end
    roll = random(1, 100);
    display_msg("<test> the addiction roll was:"+roll);

    // Drug reliant: double chance
    if has_trait(TRAIT_TRAIT, dude_obj, TRAIT_drug_addict) then chance = chance * 2;
    // Drug resistant: half chance
    if has_trait(TRAIT_TRAIT, dude_obj, TRAIT_drug_resistant) then chance = chance * 0.5;
    if is_booze(item) then chance = chance * 2;
    display_msg("Final chance was"+chance);
    if (roll > chance) then return;

    display_mymsg(MSG_became_addicted);
    set_critter_stat(dude_obj, STAT_ag, -1);
    set_critter_stat(dude_obj, STAT_ch, -1);
    if not is_iface_tag_active(INTERFACE_TAG_ADDICT) then show_iface_tag(INTERFACE_TAG_ADDICT);
    set_global_var(GVAR_ALCOHOL_ADDICT, 1);
    timer_shakes = game_time;
    timer_relief = game_time;    
end

// Use from main game window (hands)
procedure use_obj_on_obj_handler begin
    variable who = get_sfall_arg_at(0);
    variable item = get_sfall_arg_at(2);
    call drink(who, item);
end

// Use from inventory
procedure use_obj_handler begin
    variable who = get_sfall_arg_at(0);
    variable item = get_sfall_arg_at(1);
    call drink(who, item);
end

procedure start begin
    // loading
    if is_loading_game then return;
    // init script once
    if game_loaded then begin
        set_global_script_repeat(60);
        set_global_script_type(3);
        count = 0;
        msg = add_extra_msg_file(MSG_FILE);
        register_hook_proc(HOOK_USEOBJON, use_obj_on_obj_handler);
        register_hook_proc(HOOK_USEOBJ, use_obj_handler);
        ndebug("initialized");
        return;
    end
    // no addiction, skip run
    if (global_var(GVAR_ALCOHOL_ADDICT) != 1) then return;

    // Below: has addiction


    // shakes
    if ((game_time - timer_shakes) >= SHAKES_TIME) then begin
        if in_world_map then force_encounter(0);
        timer_shakes = game_time;
        if (count > 0) then
            display_mymsg(MSG_cravings);
        else
            display_mymsg(MSG_shakes);

        count = count + 1;
    end

    // relief
    if ((game_time - timer_relief) >= DURATION_OF_ADDICTION) then begin
        set_global_var(GVAR_ALCOHOL_ADDICT, 0);
        set_critter_stat(dude_obj, STAT_ag, 1);
        set_critter_stat(dude_obj, STAT_ch, 1);
        display_mymsg(MSG_relieved);
        if not dude_trait(TRAIT_drug_addict) then hide_iface_tag(INTERFACE_TAG_ADDICT);
        count = 0;                                    // resetting the counter for the next addiction cycle
    end

end
