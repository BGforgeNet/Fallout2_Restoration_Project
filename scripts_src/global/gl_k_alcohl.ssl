#define SCRIPT_REALNAME "gl_k_alcohl"
#include "../headers/define.h"
#include "../headers/scripts.h"
#include "../headers/command.h"

procedure start;

variable do_once;
variable count;
variable timer_shakes;
variable timer_relief;
variable alcohol_addict = 0;
variable msg;

#define TIME_TWO_DAYS 10368000
#define TIME_ONE_WEEK 36288000
#define INTERFACE_TAG_ADDICT 4

#define MSG_FILE "k_alcohl.msg"
#define MSG_became_addicted 1
#define MSG_cravings 2
#define MSG_shakes 3
#define MSG_relieved 4

/**
 * True is `x` is an alcohol bottle, False otherwise.
 * @arg {ObjPtr} x
 */
#define is_alcohol(x) \
    (  (obj_pid(x) == PID_BEER) \
    or (obj_pid(x) == PID_BOOZE) \
    or (obj_pid(x) == PID_GAMMA_GULP_BEER) \
    or (obj_pid(x) == PID_ABBEY_BOOZE) \
    or (obj_pid(x) == PID_ROENTGEN_RUM) )

/**
 * Displays message `x` from **game** message file `msg`
 * @arg {int} x - message number
 */
#define display_mymsg(x) display_msg(message_str_game(msg, x))

/**
 * Makes PC an addict.
 */
procedure try_addiction begin
    variable who = get_sfall_arg_at(0);
    variable item = get_sfall_arg_at(2);
    variable chance;

    // target is not dude
    if (who != dude_obj) then return;
    // not alcohol
    if not is_alcohol(item) then return;
    // already addicted
    if (alcohol_addict == 1) then return;

    // player has 10% chance of becoming addicted to alcohol
    chance = random(1, 100);
    // Drug reliant: double chance
    if has_trait(TRAIT_TRAIT, dude_obj, TRAIT_drug_addict) then chance = chance * 2;
    // Drug resistant: half chance
    if has_trait(TRAIT_TRAIT, dude_obj, TRAIT_drug_resistant) then chance = chance * 0.5;
    if (chance > 10) then return;

    display_mymsg(MSG_became_addicted);
    set_critter_stat(dude_obj, STAT_pe, -2);
    if not is_iface_tag_active(INTERFACE_TAG_ADDICT) then show_iface_tag(INTERFACE_TAG_ADDICT);
    set_global_var(GVAR_ALCOHOL_ADDICT, 1);
    alcohol_addict = 1;
end

procedure start begin
    // loading
    if is_loading_game then return;
    // init script once
    if game_loaded then begin
        set_global_script_repeat(60);
        set_global_script_type(3);
        do_once = 0;
        count = 0;
        msg = add_extra_msg_file(MSG_FILE);
        register_hook_proc(HOOK_USEOBJON, try_addiction);
        ndebug("initialized");
        return;
    end
    // no addiction, skip run
    if (global_var(GVAR_ALCOHOL_ADDICT) != 1) then return;

    // Below: has addiction

    // init timers
    if (do_once != 1) then begin
        do_once = 1;
        timer_shakes = game_time;
        timer_relief = game_time;
    end

    // shakes
    if ((game_time - timer_shakes) >= TIME_TWO_DAYS) then begin
        if in_world_map then force_encounter(0);
        timer_shakes = game_time;
        if (count > 0) then
            display_mymsg(MSG_cravings);
        else
            display_mymsg(MSG_shakes);

        count = count + 1;
    end

    // relief
    if ((game_time - timer_relief) >= TIME_ONE_WEEK) then begin
        set_global_var(GVAR_ALCOHOL_ADDICT, 0);
        set_critter_stat(dude_obj, STAT_pe, 2);
        display_mymsg(MSG_relieved);
        if not dude_trait(TRAIT_drug_addict) then hide_iface_tag(INTERFACE_TAG_ADDICT);
    end

end
